## Injection of values for variables (substitution)

## Substitution

* SEP number: [SEP-0007](sep-0007.md)
* Discussion: 
  [SPARQL Errata 8](https://www.w3.org/2013/sparql-errata#errata-query-8), [issue 89](https://github.com/w3c/sparql-12/issues/89#issuecomment-547352909)
* Author: Andy Seaborne

## Abstract

`EXISTS`/`NOT EXISTS` relies on the 
["substitute" operation](https://www.w3.org/TR/sparql11-query/#defn_substitute).

There are several problems identified with the specification material.

Substitute is also used in [`LATERAL` Join](sep-0006.md).

This SEP describes the issues and proposes solutions.

-------------------

# Substitution of Variables in SPARQL

The SPARQL 1.1 algebra operation [`substitute`](https://www.w3.org/TR/sparql11-query/#defn_substitute) evaluates a graph
pattern where there are specific values for variables given by a solution mapping.
The operation is used in the evaluation of `EXISTS` and `NOT EXISTS` operations.

A similar operation is needed for a `LATERAL` join — both are exposing the specific values for
variables, row-by-row, from part of the query evaluation into a graph pattern.

This document lists problems that have been identified with the `substitute` operation, and proposes an improved substitution evaluation
process that addresses these problems based on the concept of
[Correlated Subquery](https://en.wikipedia.org/wiki/Correlated_subquery) as found in SQL.

* [Summary](#summary)
* [Identified Issues](#identified-issues)
* [An Improved "substitute" Operation](#an-improved-substitute-operation)
* [Addressing Issues](#addressing-issues)
* [Notes](#notes)

## Summary
The fundamental problem is that a variable cannot be simply replaced by a value
(an RDF Term) in all places in a graph pattern. There are some places where
SPARQL function forms and `AS` assignments require a variable. The proposal here
is to take the variable binding from the input solution, retaining the
variable in the graph pattern, and disallowing cases that can reset the variable
binding, as is already the case elsewhere in SPARQL.

## Identified Issues
This section describes the issues identified on the
SPARQL `Exists` Community group mailing list [public-sparql-exists/2016Jul/0014](https://lists.w3.org/Archives/Public/public-sparql-exists/2016Jul/0014.html).

* [Some uses of `EXISTS` are not defined during evaluation.](#issue-1-some-uses-of-exists-are-not-defined-during-evaluation))
* [Substitution happens where definitions are only for variables.](#issue-2-substitution-happens-where-definitions-are-only-for-variables)
* [Blank nodes substituted into BGPs act as variables.](#issue-3-blank-nodes-substituted-into-bgps-act-as-variables)
* [Substitution can flip `MINUS` to its disjoint-domain case.](#issue-4-substitution-can-flip-minus-to-its-disjoint-domain-case)
* [Substitution affects disconnected variables.](#issue-5-substitution-affects-disconnected-variables)

### Issue 1: Some uses of `EXISTS` are not defined during evaluation
The evaluation process in the specification is defined for graph patterns, but
there are situations where the evaluation is of an unlisted algebra form.

For example —

```sparql
FILTER EXISTS { SELECT ?y { ?y :q :c } }
```

— and —

```sparql
FILTER EXISTS { VALUES ?y { 123 } }
```

The argument to [`EXISTS`](https://www.w3.org/TR/sparql11-query/#defn_evalExists)
is not explicitly listed as a `Graph Pattern` in the table of SPARQL algebra symbols in
[section 18.2](https://www.w3.org/TR/sparql11-query/#sparqlQuery)
where the argument to `EXISTS` is listed as a [GroupGraphPattern](https://www.w3.org/TR/sparql11-query/#rGroupGraphPattern)
containing just a [subquery](https://www.w3.org/TR/sparql11-query/#subqueries)
or just [InlineData](https://www.w3.org/TR/sparql11-query/#inline-data).

### Issue 2: Substitution happens where definitions are only for variables

There are places in the SPARQL syntax and algebra where
variables are allowed, but RDF terms (constant values) are not.

For example —

```sparql
FILTER EXISTS { BIND ( :e AS ?z ) 
                { SELECT ?x { :b :p :c } }
              }
```

Both positions `AS ?z` and `SELECT ?x` must be variables.

In the algebra, this affects —
 * [`extend`](https://www.w3.org/TR/sparql11-query/#defn_extend) — related to the use of `AS` in SPARQL syntax
 * [in line data](https://www.w3.org/TR/sparql11-query/#inline-data) — related to the use of `VALUES`
 * [`BOUND`](https://www.w3.org/TR/sparql11-query/#func-bound)


### Issue 3: Blank nodes substituted into BGPs act as variables
In the [evaluation of basic graph patterns](https://www.w3.org/TR/sparql11-query/#BasicGraphPattern)
(BGPs), blank nodes
[are replaced](https://www.w3.org/TR/sparql11-query/#BGPsparql)
by RDF terms from the graph being matched, and variables are replaced by a solution
mapping from query variables to RDF terms, so that the basic graph pattern is
now a subgraph of the graph being matched.

Simply substituting a blank node for a variable in the `EXISTS`
evaluation process does not cause the basic graph pattern to be
restricted to subgraphs containing that blank node as an RDF term,
because it is mapped by an
[RDF instance mapping](https://www.w3.org/TR/2004/REC-rdf-mt-20040210/#definst)
before checking that the BGP after mapping is a subgraph of the
graph being queried.

Note that elsewhere in the evaluation of the SPARQL algebra, a solution
mapping with a binding from variable to blank node does treat blank nodes
as RDF terms. They are not mapped by an RDF instance mapping.

For example, applying —

```sparql
SELECT ?x WHERE {
    ?x :p :d .
    FILTER EXISTS { ?x :q :b . }
}
```

— against the graph `{ :c :p :d . :e :q :b }`,
the substitution for `EXISTS` produces
`BGP(:c :q :b)`, which then
matches against `:e :q :b`, because the `_:c` can be mapped to `:e` by
the RDF instance mapping that is part of the pattern instance mappings in
[18.3.1](https://www.w3.org/TR/sparql11-query/#BGPsparql).

### Issue 4: Substitution can flip `MINUS` to its disjoint-domain case

In —

```sparql
SELECT ?x WHERE {
    ?x :p :c .
    FILTER EXISTS { ?x :p :c . MINUS { ?x :p :c . } }
}
```

— on the graph `{ :d :p :c }`, the substitution from
[18.6](https://www.w3.org/TR/sparql11-query/#sparqlAlgebraEval)
ends up producing —

```
Minus( BGP( :d :p :c ), BGP( :d :p :c ) )
```

— which produces a non-empty result, because the two solution mappings for the
`MINUS` have disjoint domains, and
[18.5](https://www.w3.org/TR/sparql11-query/#sparqlAlgebra)
then dictates that the result is not empty.

### Issue 5: Substitution affects disconnected variables

In —

```sparql
SELECT ?x WHERE {
  BIND ( :d AS ?x )
  FILTER EXISTS { BIND ( :e AS ?z )
                  SELECT ?y WHERE { ?x :p :c } }
}
```

— the substitution from
[18.6](https://www.w3.org/TR/sparql11-query/#sparqlAlgebraEval)
ends up producing —

```
  Join ( Extend( BGP(), ?z :e ),
         ToMultiSet( 
             Project( ToList( BGP( :d :p :c ) ), { ?y } )
       ))
```

The `?x` inside the `SELECT ?y` is not projected out, so it is a "different" `?x`
than the outer one; changing it to another other unused name in the same query
would not normally affect the query results.

## An Improved `substitute` Operation
Evaluating
[`substitute`](https://www.w3.org/TR/sparql11-query/#defn_substitute) is
performed for a given solution mapping. For example, if a graph pattern
has one or more matches given the variable bindings of a solution mapping,
the `EXISTS` operation evaluates to `true`. We call this solution mapping
the *current row* in this description.

This section proposes an alternative mechanism.  Rather than replace each
variable by the value it is bound to in the current row, this alternative
mechanism makes the whole of the current row available at any point in the
evaluation of an `EXISTS` expression. It uses the current row to
restrict the binding of variables, at the points where variable bindings are
created during evaluation of `EXISTS`, to be those from the current row.
It makes illegal syntactic constructs that could lead to an attempt to rebind a
variable from the current row through the `AS` syntax.

Section ["Addressing Issues"](#addressing-issues) describes how this alternative
definition of `substitute` addresses each of the issues identified above.

There are 3 parts to the proposal:
 
1. Place the current row mapping variables to values (the RDF terms) so that the
   variables always get their values from the current row.
   This is the replacement for syntactic substitution in the original definition.
2. Rename inner scope variables so that variables that are only used within a
   sub-query are not affected by the current row. This reflects the fact that in
   SPARQL such variables are not present in solution mappings outside their
   sub-query.
3. Disallow syntactic forms that set variables that may already be present in the
   current row. SPARQL solution mappings can only have one binding for a variable,
   and the current row provides that binding.

### Renaming

Within sub-queries, variables with the same name can be used, but they do not
appear in the overall results of the query if they do not occur in the
projection of the sub-query. Such inner variables are not
[in-scope](https://www.w3.org/TR/sparql11-query/#variableScope) when they are
not in the output of the projection part of the inner `SELECT` expression.

Here, the `?s` is not mentioned in the projection of
`SELECT (count(*) AS ?C)`:

```sparql
SELECT * {
  ?s :value ?v .
  FILTER EXISTS {
     { SELECT (count(*) AS ?C) {
           ?s :property ?w .
       } 
     }
     FILTER ( ?C &lt; ?v )
  }
}
```

Replacing `?s` in the sub-query with, for example,
`?V1234` does not change the overall results:

```sparql
SELECT * {
  ?s :value ?v .
  FILTER EXISTS {
     { SELECT (count(*) AS ?C) {
             ?V1234 :property ?w .
       }
     }
   FILTER ( ?C &lt; ?v )
  }
}
```

Such variables can be replaced with a variable of a different name, if
that name is not used anywhere else in the query, and the same results are
obtained in the sub-query. A sub-query always has a projection as its top-most
algebra operator.

To preserve this, any such variables are renamed so they do not coincide with
variables from the current row being filtered by `EXISTS`.

The SPARQL algebra `project` operator has two components: an algebra expression,
and a set of variables for the projection.

<blockquote>
<div class="defn">
<b>Definition: <a id="defn_projmap" name="defn_projmap">Projection Expression Variable Remapping</a></b>

  <p>
For a projection algebra operation `P` with set of variables `PV`, define
a partial mapping `F` from
<a href="https://www.w3.org/TR/sparql11-query/#sparqlQueryVariables">`V`</a>,
the set of all variables, to `V` where:
</p>
<p class="defn-expr">
`F(v) = v` if `v` in `PV`<br/>
`F(v) = v1` where `v` is a variable mentioned in the project expression
       and `v1` is a fresh variable<br/>
`F(v) = v` otherwise.
</p>
Define the <dfn>Projection Expression Variable Remapping</dfn> `PrjMap(P,PV)` to
be the algebra expression `P` (and the subtree over which the projection is
defined) with `F` applied to every variable of the algebra expression `P` over
which `P` is evaluated.
</div>
</blockquote>

This process is applied throughout the graph pattern of `EXISTS`:

<blockquote>
<div class="defn">
<b>Definition: <a id="defn_varrename" name="defn_varrename">Variable Remapping</a></b>
<p>
For any algebra expression `X`, define the
<dfn>Variable Remapping</dfn> `PrjMap(X)`:
</p>
<p class="defn-expr">
`PrjMap(X) =` replace all project operations `project(P PV)` with `project(PrjMap(P,PV) PV)` for each projection in `X`.
</p>
This replacement is applied bottom-up when there are multiple project
operations in the graph pattern of `EXISTS`.
</div>
</blockquote>

Applying the renaming steps inside a sub-query does not change the solution
mappings resulting from evaluating the sub-query. Remapping is only applied to
variables not visible outside the sub-query. Renaming a variable in a SPARQL
algebra expression causes the variable name used in bindings from evaluating the
algebra expression to change. Since these are only variables that are not
visible outside the sub-query, because they do not occur in the projection, the
result of the sub-query is unchanged. SPARQL algebra expressions cannot access
the name of a variable nor introduce a variable except by remapping.

### Limitations on Assignment

SPARQL syntactic forms that attempt to bind a variable through the use of
`AS` that might already be in a solution mapping are forbidden in
SPARQL; this is covered in the syntactic restrictions of
[19.8 Grammar](https://www.w3.org/TR/sparql11-query/#sparqlGrammar),
notes 12 and 13.

This proposal adds the restriction that any variables in a current
row, the set of variables
[in-scope](https://www.w3.org/TR/sparql11-query/#variableScope)
of the expression containing `EXISTS`, cannot be assigned with the `extend`
algebra function linked to the `AS` syntax.

In addition, any use of `VALUES` in the `EXISTS` expression must not
use a variable in the current row.

### Restriction of Bindings

The proposal is to retain the variables from the current row, not
substitute them for RDF terms before evaluation, and also to restrict
the binding of the solution to the RDF term of the current row. This occurs
after renaming.

Binding for variables occurs in several places in SPARQL:
 
 * [Basic Graph Pattern Matching](https://www.w3.org/TR/sparql11-query/#BGPsparql)
 * [Property Path Patterns](https://www.w3.org/TR/sparql11-query/#PropertyPathPatterns)
 * [evaluation of algebra form `Graph(var,P)`](https://www.w3.org/TR/sparql11-query/#defn_evalGraph)
   involving a variable (from the syntax `GRAPH ?variable {&hellip;}`)
 
Note that other places where solution mappings add variables are in
the `extend` function (connected to the `AS` syntax)
and `a multiset` from `VALUES` syntax.
[Limitations on Assignment](#limitations-on-assignment)
forbid these being variables of the current row.

Restricting the RDF Terms for a variable binding is done using
inline data that is joined with the evaluation of the basic graph pattern,
property path, or graph match.

<blockquote>
<div class="defn">
<b>Definition: <a id="defn_valuesinsertion" name="defn_valuesinsertion">Values Insertion</a></b>
<p>
For solution mapping `μ`, define `Table(μ)` to be the multiset formed from `μ`.
</p>
<p class="defn-expr">
  `Table(μ) = { μ }`<br/>
  `Card[μ] = 1`
</p>
<p>
  Define the <dfn>Values Insertion</dfn> function `Replace(X, μ)` to
  replace each occurence `Y` of a 
  <a href="https://www.w3.org/TR/sparql11-query/#sparqlTranslateBasicGraphPatterns">Basic Graph Pattern</a>,
  <a href="https://www.w3.org/TR/sparql11-query/#sparqlTranslatePathExpressions">Property Path Expression</a>,
  <a href="https://www.w3.org/TR/sparql11-query/#sparqlTranslateGraphPatterns">`Graph(Var, pattern)`</a>
  in `X` with `join(Y, Table(μ))`.
</p>
</div>
</blockquote>

### Evaluation of `EXISTS`

The evaluation of `EXISTS` is defined as:

<blockquote>
<div class="defn">
<b>Definition: <a id="defn_valuesinsertion" name="defn_valuesinsertion">Evaluation of Exists</a></b>
<p>
Let `μ` be the current solution mapping for a filter, and `X` a graph pattern,
define the <dfn>Evaluation of Exists</dfn> `exists(X)`
</p>
<p class="defn-expr">
  `exists(X) = true` if `eval(D(G), Replace(PrjMap(X), μ)` is a non-empty solution sequence.
  <br/>
  `exists(X) = false` otherwise
</p>
</div>
</blockquote>

## Addressing Issues

This section addresses each issue identified, given the proposal above.

### Issue 1: Some uses of `EXISTS` are not defined during evaluation

This can be addressed by handling solution sequences as graph patterns where
needed, by adding
[`toMultiSet`](https://www.w3.org/TR/sparql11-query/#defn_algToMultiSet)
as is done for [`SubSelect`](https://www.w3.org/TR/sparql11-query/#rSubSelect)
in [18.2.2.6 Translate Graph Patterns](https://www.w3.org/TR/sparql11-query/#sparqlTranslateGraphPatterns)
with a a correction to the text at the end of
the introductory paragraph of
[Section 18.2](https://www.w3.org/TR/sparql11-query/#sparqlQuery).

```
query-errata-N:

"Section 18.2 Translation to the SPARQL Algebra" intro (end):

ToMultiSet can be used where a graph pattern is mentioned below because the
outcome of evaluating a graph pattern is a multiset.

Multisets of solution mappings are elements of the SPARQL algebra.  Multisets
of solution mappings count as graph patterns.
```

### Issue 2: Substitution happens where definitions are only for variables

Rather then replace a variable by its value in the current row, the new
mechanism makes the binding of variable to value available. The variable
remains in the graph pattern of `EXISTS` and the evaluation.

### Issue 3: Blank nodes substituted into BGPs act as variables

By making the current row, which can include blank nodes, available, and not
modifying the BGP by substitution, no blank nodes are introduced into the
evaluation of the BGP. Instead, the possible solutions are restricted by the
current row.

### Issue 4: Substitution can flip `MINUS` to its disjoint-domain case

Issue 4 is addressed because variables are not removed from the domain of
`MINUS`. This proposal does not preserve all uses of `MINUS`
expressions; the problem identified in issue 4 is considered to be a bug in the
SPARQL 1.1 specification.

### Issue 5: Substitution affects disconnected variables

Issue 5 is addressed by noting that variables inside sub-queries which are not
projected can be renamed without affecting the sub-query results. Whether to
preserve that invariant or allow the variables to be set by the current row is a
choice point; this design preserves the independence of disconnected variables.

## Notes

The proposal described in this document does not cover the use of variables from
the current row in a `HAVING` clause.

### Notes

Original content: https://afs.github.io/substitute.html

See also [Correlation and Substitution in SPARQL](https://arxiv.org/pdf/1606.01441.pdf).
